plugins {
    id 'java'
    id 'maven-publish'
    id 'com.enonic.defaults' version '2.0.1'
    id 'com.enonic.xp.app' version '2.0.0'
    id "com.github.node-gradle.node" version '2.2.4'
}

def xpVersion=version

app {
    name = 'com.enonic.xp.app.users'
    group = 'com.enonic.xp'
    displayName = 'Users'
    systemApp = true
    systemVersion = "${version}"
    devSourcePaths += file("$rootDir/../lib-admin-ui/src/main/resources")
}

dependencies {
    compile("${group}:core-api:${xpVersion}") {
        force = true
    }
    compile ("${group}:portal-api:${xpVersion}") {
        force = true
    }
    include "${group}:lib-admin:${xpVersion}"
    include "${group}:lib-portal:${xpVersion}"
    include "${group}:lib-repo:${xpVersion}"
    include "${group}:lib-context:${xpVersion}"
    include "${group}:lib-node:${xpVersion}"
    include "${group}:lib-auth:${xpVersion}"
    include "${group}:lib-io:${xpVersion}"
    include "${group}:lib-value:${xpVersion}"
    include "${group}:lib-i18n:${xpVersion}"
    include "${group}:lib-task:${xpVersion}"
    include "${group}:lib-websocket:${xpVersion}"
    include "${group}:lib-event:${xpVersion}"
    include "com.graphql-java:graphql-java:2.3.0"
    include "com.enonic.lib:lib-graphql:1.0.0"
    include "com.enonic.lib:lib-admin-ui:${libAdminUiVersion}"
    include "com.enonic.lib:lib-mustache:2.0.0"
    testCompile "${group}:testing:${xpVersion}"
    testCompile 'junit:junit:4.13.1'
    testCompile 'org.mockito:mockito-core:3.7.7'
}

configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

repositories {
    mavenLocal()
    jcenter()
    xp.enonicRepo('dev')
}

node {
    download = true
    version = '14.15.0'
}


task flush( type: org.gradle.api.tasks.Delete, dependsOn: clean ) {
    description = 'Clean the project from built sources and dependencies'
    delete '.xp'
}

task npmInstallForce( type: NpmTask ) {
    description = 'Update all project node dependencies'
    args = ['install', '--force']
}

task cleanNode( type: org.gradle.api.tasks.Delete ) {
    delete 'node_modules'
}

npmInstall.mustRunAfter unpackDevResources

task lint( type: NpmTask, dependsOn: npmInstall ) {
    args = [ 'run', 'lint' ]
    inputs.files fileTree( dir: 'modules', include: '**/src/main/**.*' )
    outputs.dir file('gradle')
    outputs.upToDateWhen { false }
}

task webpack( type: NpmTask, dependsOn: [npmInstall, lint] ) {
    environment = [ 'NODE_ENV': nodeEnvironment() ]
    description = 'Build UI resources (ts, css, etc).'
    args = [ 'run', 'build' ]
    inputs.dir '.xp/dev'
    inputs.dir 'src/main'
    inputs.file 'webpack.config.js'
    outputs.dir "${buildDir}/resources/main"
}

jar {
    exclude 'assets/**/*.ts'
    exclude 'assets/**/*.less'

    exclude 'assets/styles/*.js'

    outputs.dir "${buildDir}/resources/main"

    dependsOn += webpack
}

project.afterEvaluate {
    jar {
        bnd ('Private-Package': '!dev.*, *;-split-package:=merge-first')
    }
}

processResources.dependsOn += unpackDevResources

if ( hasProperty( 'env' ) )
{
    addBuildDependency()
    applyExcludedTasks()
}

def applyExcludedTasks() {
    if ( isLibAdminUiIncluded() )
    {
        def libAdminUi = gradle.includedBuild( 'lib-admin-ui' )
        def excludedTasks = gradle.startParameter.getExcludedTaskNames().findAll( { it == 'lint' || it == 'test' } )
        libAdminUi.getLoadedSettings().getStartParameter().setExcludedTaskNames( excludedTasks )
    }
}

def addBuildDependency() {
    if ( isLibAdminUiIncluded() )
    {
        def buildTask = gradle.includedBuild( 'lib-admin-ui' ).task( ':build' )

        build.dependsOn += buildTask
        clean.dependsOn += gradle.includedBuild( 'lib-admin-ui' ).task( ':clean' )
        flush.dependsOn += gradle.includedBuild( 'lib-admin-ui' ).task( ':flush' )

        def webpackTask = tasks.findByPath( ':webpack' )
        def unpackDevResources = tasks.findByPath( ':unpackDevResources' )

        if ( webpackTask != null ) webpackTask.dependsOn += unpackDevResources
        unpackDevResources.dependsOn += buildTask
    }
}

def isLibAdminUiIncluded() {
    return new File( '../lib-admin-ui' ).exists()
}

def nodeEnvironment() {
    def environments = [ prod : 'production', dev: 'development' ]
    def nodeEnv = environments[hasProperty( 'env' ) ? env : 'prod']
    return nodeEnv != null ? nodeEnv : 'production'
}
