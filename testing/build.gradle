import com.enonic.uitest.server.ServerInstance

plugins {
    id 'java'
    id 'com.enonic.defaults'
    id 'com.enonic.xp.base'
    id 'com.github.node-gradle.node'
}
group = 'com.enonic.xp.js_testing'

repositories {
    mavenCentral()
    xp.enonicRepo('dev')
}

configurations {
    distro
}

enonic {
    publishRepo = 'inhouse'
}

ext {
    unpackDir = "$buildDir/install"
    distroDir = "$unpackDir/enonic-xp-generic-$version"
    deployDir = "$distroDir/home/deploy"
    testDataDir = "$projectDir/test-data/common-config"
    resDir = "$projectDir/test-data"
    appDir = "$projectDir/test-data/test-applications"
    xpHome = "$distroDir/home"
    configDir = "$xpHome/config"
    xpServer = new ServerInstance()
    appName = "app-users"
    appFile = "$distroDir/system/40/${appName}-${version}.jar"
    appUrl = project.hasProperty('appUrl') ? appUrl : "file:///$projectDir/../build/libs/${appName}.jar"
}

dependencies {
    distro "com.enonic.xp:enonic-xp-generic:$version@zip"
}

task unpackDistro( type: Copy ) {
    from {
        configurations.distro.collect { zipTree( it ) }
    }
    into file( unpackDir )
}

task copyConfig( type: Copy ) {
    println testDataDir
    from testDataDir
    include '**.*.cfg'
    include '*.properties'
    into file( configDir )
    mustRunAfter unpackDistro
}

task copyApps( type: Copy ) {
    println appDir
    from file( appDir )
    include '*.jar'
    into file( deployDir )
    mustRunAfter unpackDistro
}

task deployApp( type: DefaultTask ) {
    outputs.files( appFile )
    outputs.upToDateWhen { false }
    doLast {
        def f = new File( appFile )
        println "Deleting  ${appFile}. Exists ${f.exists()}"

        f.delete()

        println "Copying from ${appUrl} to ${appFile}"
        new URL( appUrl ).withInputStream { i -> f.withOutputStream { it << i } }
    }
    mustRunAfter unpackDistro
}

task startServer( dependsOn: unpackDistro ) {
    doLast {
        xpServer.installDir = file( distroDir )
        xpServer.start()
    }
}

task stopServer {
    doLast {
        xpServer.stop()
    }
}

// wdio_chrome is a script defined in package.json
task w_testUsersApp( type: NpmTask, dependsOn: [npmInstall, unpackDistro, copyConfig, copyApps, deployApp, startServer] ) {
    args = ['run-script', 'test_users:wdio_chrome']
    finalizedBy stopServer
}

task w_testUsersAppLocal( type: NpmTask, dependsOn: [npmInstall] ) {
    args = ['run-script', 'test_users:wdio_chrome']
}
/////////////////////////
task installGeckoService(type: NpmTask) {
    // install the express package only
    args = ['install', 'wdio-geckodriver-service', '--save-dev']
}
task installGeckoDriver(type: NpmTask) {
    // install the express package only
    args = ['install', 'geckodriver', '--save-dev']
}
task testUsersAppFirefox( type: NpmTask, dependsOn: [npmInstall,installGeckoService, installGeckoDriver, unpackDistro, copyConfig, copyApps, deployApp, startServer] ) {
    args = ['run-script', 'test:firefox']
    finalizedBy stopServer
}

task testUsersAppFirefoxLocally( type: NpmTask, dependsOn: [npmInstall] ) {
    args = ['run-script', 'test:firefox']
}

